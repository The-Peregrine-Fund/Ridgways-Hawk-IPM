
    model{
      lmu.brood ~ dnorm(0, 1/(10*10) )
      delta ~ dnorm(0, 1/(10*10))
      gamma ~ dnorm(0, 1/(10*10))
      logit(lmu.nest) <- mu.nest
      mu.nest ~ dbeta(1,1) 
      sig.brood ~ dnorm(0, 1/(2*2)) T(0,)
      sig.nest ~ dnorm(0, 1/(2*2)) T(0,)
      sig ~ dnorm(0, 1/(2*2)) T(0,)
      
      # Two models for fecundity, (1) brood size and (2) nest success  
      # Brood size
      for (k in 1:nbrood){
        brood[k] ~ dnorm(lam[k], 1/(sig*sig)) T( mintrunc, maxtrunc) # truncated at the max observed fledglings per territory (3) 
        #brood[k] ~ dpois(lam[k]) T( mintrunc, maxtrunc) # truncated at the max observed fledglings per territory (3) 
        lam[k] <- lmu.brood + 
                        delta*treat.brood[k] + 
                        eps.brood[ year.brood[k] ]
      } # k
      # Nest success       
      for (n in 1:nnest){
        nest.success[n] ~ dbern( nu[n] )
        logit(nu[n]) <- lmu.nest + gamma*treat.nest[n] + eps.nest[ year.nest[n] ]
      } # n 
      
      for ( t in 1:nyr){
        eps.brood[t] ~ dnorm(0, 1/(sig.brood*sig.brood) )
        eps.nest[t] ~ dnorm(0, 1/(sig.nest*sig.nest) )
      } # t
      # summarize yearly brood size for population model
      # accounts for nest treatments
      for (t in 1:nyr){
      for (xx in 1:brood.end[t]){
        broodmat[t,xx] <- lam[yrind.brood[xx,t]]
      } # xx
      for (xxx in 1:nest.end[t]){
          nestmat[t,xxx] <- nu[yrind.nest[xxx,t]]
        } # xxx
        mn.brood[t] <- mean(broodmat[t,1:brood.end[t]])
        mn.nest[t] <- mean(nestmat[t,1:nest.end[t]])
        mn.f[t] <- mn.brood[t]*mn.nest[t] # calc fecundity
      } # t
} # model

